Model:
    (('INIT' 'STATE' init_state=[State])
    (serial=Serial)?
    ('FREQUENCY' frequency=FLOAT)?)#
    bricks+=Brick
    states+=State
;

DigitalValue:
    value='ON' | value='OFF'
;

Brick:
    Sensor | Actuator | Serial
;

Sensor:
    AnalogicSensor | DigitalSensor
;

Actuator:
    AnalogicActuator | DigitalActuator | LiquidCrystal
;

AnalogicSensor :
    'ANALOG' 'SENSOR' name=ID pin=INT
;

DigitalSensor :
    'DIGITAL' 'SENSOR' name=ID pin=INT
;

AnalogicActuator :
    'ANALOG' 'ACTUATOR' name=ID pin=INT
;

DigitalActuator :
    'DIGITAL' 'ACTUATOR' name=ID pin=INT
;

LiquidCrystal :
    'LiquidCrystal' name=ID  pin+=INT 'size[' matrix_size*=INT[','] ']'
;

Serial :
    'SERIAL' name=ID 'BAUDRATE' baudrate=INT
;

State:
    'STATE' name=ID '{'
        statements*=Statements
    '}'
;

Statements :
    Action |
    Transition
;

Action :
    Assignment | Print | Wait
;

Print:
    cmd='PRINTLN' output=[PrintableDevice] from_brick=[Brick] |
    cmd='PRINTLN' output=[PrintableDevice] msg=STRING |
    cmd='PRINT' output=[PrintableDevice] from_brick=[Brick] |
    cmd='PRINT' output=[PrintableDevice] msg=STRING

;

PrintableDevice:
    Serial | LiquidCrystal
    ;

Wait:
    'WAIT' milli=INT
;

Transition:
    condition=Condition '=>' exception=Exception |
    condition=Condition '=>' 'STATE' next_state=[State] |
    '=>' 'STATE' next_state=[State] |
    '=>' exception=Exception
;

Assignment :
    var=[Actuator] '=' value=DigitalValue |
    var=[Actuator] '=' value=INT |
    var_analog=[AnalogicActuator] '=' value=FLOAT |
    var=[Actuator] '=' new_value=[Sensor]
;

Exception:
    'ERROR' value=INT
;

Condition:
    l=ConditionTerm op=BooleanOperators r=Condition |
    l=ConditionTerm op=BooleanOperators r=ConditionTerm |
    l=ConditionTerm
;

ConditionTerm:
    serial=[Serial] '==' r=STRING |
    l=[Brick] op=Operators r=Comparable |
    l=[Brick] op=Operators r=DigitalValue
;

Comparable:
    value=INT |
    value=FLOAT |
    value=STRING
;

Operators:
    '==' | '!=' | '>=' | '<='
;

BooleanOperators:
    '&&' | '||'
;
