Model:
    ("FREQUENCY" freq=FLOAT)?
    bricks*=Brick
    states*=State
;

Brick:
    'ACTUATOR' name=ID child=Actuator |
    'SENSOR' name=ID  child=Sensor |
    'LCD' name=ID child=Lcd |
    'SERIAL' name=ID child=Serial
;

Actuator:
    pin=INT is_analogic?="ANALOGIC"
;

Sensor:
    pin=INT is_analogic?="ANALOGIC"
;

Lcd:
    'BUS' bus_id=INT
;

Serial:
    'BAUDRATE' baudrate=INT
;


DigitalValue:
    value='ON' | value='OFF'
;

Action:
    type='SET' var=ID value=DigitalValue |
    type='SET' var=ID value=ID |
    type='PRINTLN' var=ID value=ID |
    type='PRINTLN' var=ID value=STRING |
    type='PRINT' var=ID value=ID |
    type='PRINT' var=ID value=STRING |
    type='WAIT' value=INT |
    type='CLEAR' var=ID |
    Exception
;

Exception:
    'ERROR' value=INT
;

Transition:
    cond=Condition '->' next_state=Exception |
    cond=Condition '->' next_state=ID
;

State:
    is_init_state?='INIT' name=ID '{'
    exprs*=Exprs
    '}'
;

Exprs:
    Action | Transition
;

Condition:

    l=Bexpr op=Bop r=Condition |
    l=Bexpr op=Bop r=Bexpr |
    single=Bexpr |
    empty?=""
;

Bop:
    token='AND' | token='OR'
;

Bexpr:
    var=ID op=CompOp value=DigitalValue |
    var=ID op=CompOp value=STRING |
    var=ID op=CompOp value=FLOAT
;

CompOp:
    '==' | '>=' | '<=' | '!='
;